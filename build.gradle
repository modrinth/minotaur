plugins {
	id 'com.gradle.plugin-publish' version '1.2.0'
}

version = 'v2.9.0-test.1'
group = 'com.modrinth.minotaur'
archivesBaseName = 'Minotaur'
description = 'Modrinth plugin for publishing builds to the website!'

String vendor = 'Modrinth'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
	mavenCentral()
	gradlePluginPortal()
}

dependencies {
	compileOnly gradleApi()
	compileOnly group: 'org.jetbrains', name: 'annotations', version: '+'
	api group: 'dev.masecla', name: 'Modrinth4J', version: '2.2.0'
	api group: 'org.ow2.asm', name: 'asm', version: '9.5'
	api group: 'org.ow2.asm', name: 'asm-tree', version: '9.5'
	compileOnly group: 'io.papermc.paperweight', name: 'paperweight-userdev', version: '1.5.2'
}

gradlePlugin {
	website = 'https://modrinth.com'
	vcsUrl = 'https://github.com/modrinth/minotaur'
	plugins {
		minotaur {
			id = 'com.modrinth.minotaur'
			implementationClass = 'com.modrinth.minotaur.Minotaur'
			displayName = 'Minotaur'
			description = 'Modrinth plugin for publishing builds to the website!'
			tags.addAll('modrinth', 'publish', 'mods', 'minecraft')
		}
	}
}

jar {
	manifest {
		attributes([
			'Timestamp'               : System.currentTimeMillis(),
			'Specification-Title'     : project.archivesBaseName,
			'Specification-Vendor'    : vendor,
			'Specification-Version'   : project.version,
			'Implementation-Title'    : project.archivesBaseName,
			'Implementation-Version'  : project.version,
			'Implementation-Vendor'   : vendor,
			'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})"
		])
	}
}

java {
	withSourcesJar()
	withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release.set(8)
}
