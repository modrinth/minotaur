plugins {
    id 'com.gradle.plugin-publish' version '1.0.0-rc-2'
}

version = '2.3.1'
group = 'com.modrinth.minotaur'
archivesBaseName = 'Minotaur'
description = 'Modrinth plugin for publishing builds to the website!'

String vendor = 'Modrinth'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven {
        name = "Fabric"
        url = "https://maven.fabricmc.net"
        content {
            includeGroup "net.fabricmc"
        }
    }
}

dependencies {
    compileOnly gradleApi()

    compileOnly group: 'net.fabricmc', name: 'fabric-loom', version: '0.12.48'

    implementation group: 'org.apache.httpcomponents', name: 'httpmime', version: '4.5.13'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.0'

    compileOnly group: 'org.jetbrains', name: 'annotations', version: '+'
}

gradlePlugin {
    plugins {
        minotaur {
            id = 'com.modrinth.minotaur'
            implementationClass = 'com.modrinth.minotaur.Minotaur'
            displayName = 'Minotaur'
        }
    }
}

pluginBundle {
    website = 'https://modrinth.com'
    vcsUrl = 'https://github.com/modrinth/minotaur'
    description = 'Modrinth plugin for publishing builds to the website!'
    tags = ['modrinth', 'publish', 'mods', 'minecraft']
}

jar {
    manifest {
        attributes([
                'Timestamp'               : System.currentTimeMillis(),
                'Specification-Title'     : project.archivesBaseName,
                'Specification-Vendor'    : vendor,
                'Specification-Version'   : project.version,
                'Implementation-Title'    : project.archivesBaseName,
                'Implementation-Version'  : project.version,
                'Implementation-Vendor'   : vendor,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Built-On-Java'           : "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})"
        ])
    }
}

java {
    withSourcesJar()
    withJavadocJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release.set(8)
}
